group 'com.comiit.demo.hdinsight'
version '1.0-SNAPSHOT'

apply plugin: 'scala'
apply plugin: 'idea'
apply plugin: 'provided-base'
apply plugin: 'eu.appsatori.fatjar'


ext.scalaVersion = '2.10.0'
ext.scaldingVersion = '0.13.1'
ext.cascadingVersion = '2.7.0'
ext.hadoopVersion = '2.6.0'

buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.1'
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'http://conjars.org/repo/' }
    maven { url 'http://maven.twttr.com/' }
}

dependencies {

    compile(group: 'org.scala-lang', name: 'scala-library', version: scalaVersion)
    compile(group: 'org.scala-lang', name: 'scala-compiler', version: scalaVersion)

    compile(group: 'com.twitter', name: 'scalding-core_2.10', version: scaldingVersion) {
        exclude group: 'cascading', module: 'cascading-hadoop'
    }

    compile(group: 'cascading', name: 'cascading-core', version: cascadingVersion)
    compile(group: 'cascading', name: 'cascading-hadoop2-mr1', version: cascadingVersion)

    provided(group: 'org.apache.hadoop', name: 'hadoop-common', version: hadoopVersion)
    provided(group: 'org.apache.hadoop', name: 'hadoop-mapreduce-client-core', version: hadoopVersion)

    compile(group: 'com.twitter', name: 'chill_2.10', version: '0.3.6')
}

task distTargz(type: Tar) {
    dependsOn jar, compileScala

    compression = Compression.GZIP
    baseName = 'hdinsight-demo'
    extension = 'tar.gz'
    classifier = 'distribution'

    into("${baseName}/lib") {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        from { jar.archivePath }
    }

    into(baseName) {
        from { "README.md" }
        from { "log4j.properties" }
    }

    into("${baseName}/scripts") {
        from { 'scripts' }
    }
}

task libraries(type: Copy) {
    from { configurations.distrib }
    from { jar.archivePath }

    into { "lib" }
}

fatJar {
    classifier 'fat'
    manifest {
        attributes("Main-Class": "com/twitter/scalding/Tool")
    }
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF', '*/build/**'
}
